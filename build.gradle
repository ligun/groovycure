plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'groovycure'
group = 'net.ligun'
version = '1.1.2'

def javaVersion = 8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

test {
    useJUnitPlatform()
    javaLauncher.set(javaToolchains.launcherFor {
        def launcherVersion = project.hasProperty('javaVersion') ? Integer.parseInt(project.property('javaVersion')) : javaVersion
        languageVersion = JavaLanguageVersion.of(launcherVersion)
    })
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            pom {
                name = archivesBaseName
                description = 'Pretty Cure'
                url = 'https://github.com/ligun/groovycure'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                    }
                }
                developers {
                    developer {
                        id = developperId
                        name = developperName
                        email = developperEmail
                    }
                }
                scm {
                    connection = 'git@github.com:ligun/groovycure.git'
                    developerConnection = 'git@github.com:ligun/groovycure.git'
                    url = 'https://github.com/ligun/groovycure'
                }
                repositories {
                    maven {
                        def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                        def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = sonatypeUsername
                            password = sonatypePassword
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "file:${buildDir}"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
