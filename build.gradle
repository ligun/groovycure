plugins {
    id 'groovy'
    id 'maven-publish'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

archivesBaseName = 'groovycure'
group = 'groovycure'
mainClassName = 'groovycure.Precure'
version = '0.0.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation module('org.codehaus.groovy:groovy-all:2.5.14') {
        dependency('org.codehaus.groovy:groovy-dateutil:2.5.14')
        dependency('org.codehaus.groovy:groovy-cli-commons:2.5.14')
    }
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "file:${buildDir}"
        }
    }
}

run {
    if(project.hasProperty('args')) {
        args project.args.split(/s+/)
    }
}

task makeExecutableJar(type: Jar, dependsOn: shadowJar) {
    manifest {
        attributes 'Main-Class': 'groovycure.Precure'
    }
    doLast {
        def jarshDir = new File("${buildDir}/jarsh")
        jarshDir.mkdir()

        def shell = new File("${jarshDir}/precure")
        def bat = new File("${jarshDir}/precure.bat")
        def jar = new File("${buildDir}/libs/${archivesBaseName}-${version}-all.jar")

        shell.text = """\
#!/bin/sh
java -jar "\$0" "\$@"
exit \$?
"""
        shell << jar.bytes

        bat.text = """\
@echo off
java -jar "%0" %*\r\n\
exit /B 1\r\n\
"""
        bat << jar.bytes
    }
}
